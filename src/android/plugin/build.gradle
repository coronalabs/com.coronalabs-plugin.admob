apply plugin: 'com.android.library'

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 32
    }
}

afterEvaluate {
    generateReleaseBuildConfig.enabled = false
    generateDebugBuildConfig.enabled = false
}

dependencies {
    implementation ':Corona@aar'
    implementation 'com.google.android.gms:play-services-ads:22.5.0'
    implementation 'com.google.android.ump:user-messaging-platform:2.1.0'
}

task exportAARToThePluginBinary {
    group 'Solar2Dev'
    dependsOn assemble
    doLast {
        copy {
            from {
                zipTree("$buildDir/outputs/aar/${project.name}-release.aar").matching {
                    include 'classes.jar'
                }.singleFile
            }
            into "../../../plugins/2020.3607/android/"
            rename "classes.jar", "plugin.admob.jar"
        }
    }
}

task createDataTgz(type: Tar) {
    group 'Solar2Dev'
    from "../../../plugins/2020.3607/android/"
    destinationDirectory = file("$buildDir/outputs")
    archiveFileName = 'data.tgz'
    compression = Compression.GZIP
    dependsOn exportAARToThePluginBinary
}

assemble.finalizedBy(createDataTgz)
